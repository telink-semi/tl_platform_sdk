/********************************************************************************************************
 * @file    ram_boot.link
 *
 * @brief   This is the link file for tl323x
 *
 * @author  Driver Group
 * @date    2025
 *
 * @par     Copyright (c) 2025, Telink Semiconductor (Shanghai) Co., Ltd. ("TELINK")
 *
 *          Licensed under the Apache License, Version 2.0 (the "License");
 *          you may not use this file except in compliance with the License.
 *          You may obtain a copy of the License at
 *
 *              http://www.apache.org/licenses/LICENSE-2.0
 *
 *          Unless required by applicable law or agreed to in writing, software
 *          distributed under the License is distributed on an "AS IS" BASIS,
 *          WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *          See the License for the specific language governing permissions and
 *          limitations under the License.
 *
 *******************************************************************************************************/
ENTRY(_RAM_RESET_ENTRY)
SECTIONS
{
    NDS_SAG_LMA_FLASH = 0x00068000 ;
    . = 0x00068000;
    
    PROVIDE (BIN_BEGIN = .);
    
    .ram_boot   : { KEEP(*(.ram_boot )) }

    . = ALIGN(8);
    .retention_data : 
        { KEEP(*(.retention_data )) }
    . = ALIGN(8);
    .ram_code   : 
        { KEEP(*(.ram_code )) KEEP(*libgcc.a:save-restore.o) KEEP(*(.exec.itable)) }

    .text       : 
        { *(.text .stub .text.* .gnu.linkonce.t.* ) KEEP(*(.text.*personality* )) *(.gnu.warning ) . = ALIGN(4); }
        
    .eh_frame_hdr   : 
        { *(.eh_frame_hdr ) }
    . = ALIGN(0x20);
    .eh_frame   :
        { KEEP(*(.eh_frame )) }

    . = ALIGN(8);
    PROVIDE( __global_pointer$ = . + (4K / 2) );
    .rf_certification_cfg :
        { KEEP(*(.rf_certification_cfg)) }
    PROVIDE (_RF_CERTIFICATION_CFG_ADDR_OFFSET = LOADADDR(.rf_certification_cfg) - BIN_BEGIN);
    . = ALIGN(8);
    .data   : 
        { *(.data .data.* .gnu.linkonce.d.* ) KEEP(*(.gnu.linkonce.d.*personality* )) SORT(CONSTRUCTORS) 
          *(.srodata.cst16 ) *(.srodata.cst8 ) *(.srodata.cst4 ) *(.srodata.cst2 ) *(.srodata .srodata.* ) *(.sdata .sdata.* .gnu.linkonce.s.* ) *(.sdata2 .sdata2.* .gnu.linkonce.s.* )
          *(.rodata .rodata.* .gnu.linkonce.r.* )
        }
    . = ALIGN(8);
    PROVIDE (_BSS_VMA_START = .);
    .sbss   (NOLOAD)    : { *(.dynsbss ) *(.sbss .sbss.* .gnu.linkonce.sb.* ) *(.scommon .scommon.* ) }
    .bss    (NOLOAD)    : { *(.dynbss ) *(.bss .bss.* .gnu.linkonce.b.* ) *(COMMON ) . = ALIGN(8); }
    /* When using the S file _FILL_STK function (which clears all the values in the stack space to 0x55), 
     * if _BSS_VMA_END does not have four-byte alignment, there may be an out-of-bounds access beyond _STACK_TOP,
     * resulting in a store access fault.
     */
    PROVIDE (_BSS_VMA_END = ((. + 3) / 4)*4);
    
    .sdk_version    : AT(LOADADDR (.data) + SIZEOF (.data))
        { KEEP(*(.sdk_version )) }
    
    . = ALIGN(8);
    /* end is the starting address of the heap, the heap grows upward */
    _end = .;
    PROVIDE (end = .);  
    PROVIDE (_STACK_TOP = 0x00080000 + 0x8000 * __D25F_DRAM_SIZE);
    PROVIDE (_ILM_USE_START = ADDR(.ram_boot));
    PROVIDE (_ILM_USE_END = _BSS_VMA_END);
    PROVIDE (_ILM_USE_SIZE = (_ILM_USE_END) -(_ILM_USE_START));
}
ASSERT((_ILM_USE_SIZE)<= (_STACK_TOP-600), "DRAM OVERFLOW");/*Error: RAM size maybe overflow.(This is determined based on the stack size of 600 bytes. If this does not match the actual usage, the corresponding judgment criteria in the link file can be modified*/